import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'groovy'

defaultTasks 'distPlugin'

def apiVersion="3"
def pluginName="uc_email_plugin"
def pluginVersion="dev"
def buildLocaleDir = 'build/locale'

configurations {
    // Remove the groovy-all jar from runtime dependencies
    runtime.exclude module: 'groovy-all'
    runtime.exclude module: "groovy-xml"
    ucbuild
    ucdeploy
}

configurations.all {
    exclude module: "groovy-all"
    exclude module: "groovy-xml"
    resolutionStrategy {
        force "org.codehaus.groovy:groovy:${GroovySystem.version}"
        force "org.codehaus.groovy:groovy-all:${GroovySystem.version}"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.ibm.urbancode.plugins:groovy-plugin-utils:1.0'
    compile 'com.ibm.urbancode.plugins:uDeployRestClient:1.0'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    compile 'javax.mail:mail:1.4.1'
    compile gradleApi()
    compile localGroovy()
    testCompile "org.spockframework:spock-core:1.1-groovy-2.4"
}

test {
    testLogging.showStandardStreams = true
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src/main/groovy']
        }
    }

    test {
        groovy {
            srcDirs = ['src/test/groovy']
        }
    }

    ucbuild {
        groovy {
            srcDirs = ['src/ucbuild/zip']
        }
    }

    ucdeploy {
        groovy {
            srcDirs = ['src/ucdeploy/zip']
        }
    }
}

task copyDeps(type: Copy) {
    from configurations.runtime
    into "${buildDir}/copylibs"
}

task distPlugin(dependsOn: ['copyDeps', 'distPluginUCB', 'distPluginUCD'])

tasks.create([name: "distPluginUCB", type: Zip]) {
    dependsOn = ['compileGroovy']

    def releaseVersion = apiVersion + "." + pluginVersion
    into('classes') {
        from(sourceSets.main.groovy.srcDirs) {
            filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
        }
    }
    /*from(sourceSets.main.groovy.srcDirs) {
      filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
    }*/
    from(sourceSets.ucbuild.groovy.srcDirs) {
        filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
    }
    into('lib') {
        from copyDeps
    }
    into('locale') {
        from buildLocaleDir
    }
    archiveName = "${pluginName}-build-${releaseVersion}.zip"
}

tasks.create([name: "distPluginUCD", type: Zip]) {
    dependsOn = ['compileGroovy']
    def releaseVersion = apiVersion + "." + pluginVersion
    into('classes') {
        from(sourceSets.main.groovy.srcDirs) {
            filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
        }
    }
    from(sourceSets.ucdeploy.groovy.srcDirs) {
        filter(ReplaceTokens, tokens: [API_VERSION: apiVersion, RELEASE_VERSION: releaseVersion])
    }
    into('lib') {
        from copyDeps
    }
    into('locale') {
        from buildLocaleDir
    }
    archiveName = "${pluginName}-deploy-${releaseVersion}.zip"
}

task cleanAll(dependsOn: ['clean', 'cleanCopyDeps', 'cleanDistPlugin'])
